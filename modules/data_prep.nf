process FETCH_GENOMES {
    label "needsInternet"
    publishDir params.publishDir

    output:
    path params.read_simulation.sampleGenomes

    script:
    """
    qiime rescript get-ncbi-genomes \
      --verbose \
      --p-taxon ${params.read_simulation.taxon} \
      --p-assembly-levels complete_genome \
      --p-assembly-source refseq \
      --o-genome-assemblies ${params.read_simulation.sampleGenomes} \
      --o-loci "sample-loci.qza" \
      --o-proteins "sample-proteins.qza" \
      --o-taxonomies "sample-taxonomy.qza"
    """
}

process SIMULATE_READS {
    label "readSimulation"
    scratch true
    tag "${sample_id}"
    errorStrategy 'retry'
    maxRetries 3

    input:
    tuple val(sample_id), path(genomes)

    output:
    tuple val(sample_id), path(reads), emit: reads
    tuple val(sample_id), path(output_genomes), emit: genomes
    tuple val(sample_id), path(abundances), emit: abundances

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    reads = "${params.runId}_reads_${sample_id}"
    output_genomes = "${params.runId}_output_genomes_${sample_id}"
    abundances = "${params.runId}_output_abundances_${sample_id}"
    """
    if [ ! -d "${q2cacheDir}" ]; then
      qiime tools cache-create --cache ${q2cacheDir}
    fi

    qiime assembly generate-reads \
      --verbose \
      --i-genomes ${genomes} \
      --p-sample-names ${sample_id} \
      --p-cpus ${task.cpus} \
      --p-n-genomes ${params.read_simulation.nGenomes} \
      --p-n-reads ${params.read_simulation.readCount} \
      --p-seed ${params.read_simulation.seed} \
      --p-abundance ${params.read_simulation.abundance} \
      --p-gc-bias ${params.read_simulation.gc_bias} \
      --o-reads ${q2cacheDir}:${reads} \
      --o-template-genomes ${q2cacheDir}:${output_genomes} \
      --o-abundances ${q2cacheDir}:${abundances} \
    && touch ${reads} \
    && touch ${output_genomes} \
    && touch ${abundances}
    """
}

process SIMULATE_READS_MASON {
    label "readSimulation"
    scratch true
    tag "${sample_id}"
    errorStrategy 'retry'
    maxRetries 3
    storeDir params.storeDir
    clusterOptions params.read_simulation.clusterOptions

    input:
    tuple val(sample_id), val(abundance_profile), val(read_count), val(read_length), path(genomes)

    output:
    tuple val(sample_id), path(reads), path(table), emit: reads

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    reads = "${params.runId}_reads_${sample_id}"
    table = "${params.runId}_mason_ft_${sample_id}"
    """
    if [ ! -d "${q2cacheDir}" ]; then
      qiime tools cache-create --cache ${q2cacheDir}
    fi

    qiime assembly simulate-reads-mason \
      --verbose \
      --i-reference-genomes ${genomes} \
      --p-sample-names ${sample_id} \
      --p-abundance-profiles ${abundance_profile} \
      --p-num-reads ${read_count} \
      --p-read-length ${read_length} \
      --p-random-seed ${params.read_simulation.seed} \
      --p-threads ${task.cpus} \
      --o-reads ${q2cacheDir}:${reads} \
      --o-table ${q2cacheDir}:${table} \
    && touch ${reads} \
    && touch ${table}
    """
}

process FETCH_SEQS {
    label "fondue"
    label "needsInternet"
    scratch true
    tag "${_id}"
    errorStrategy { task.exitStatus in [125, 126] ? 'ignore' : 'retry' }
    maxRetries 3

    input:
    val _id
    // path q2_cache

    output:
    tuple val(_id), path(reads_single), emit: single
    tuple val(_id), path(reads_paired), emit: paired
    tuple val(_id), path(failed_runs), emit: failed

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${_id}"
    reads_paired = "${params.runId}_reads_paired_${_id}"
    reads_single = "${params.runId}_reads_single_${_id}"
    failed_runs = "${params.runId}_failed_runs_${_id}"
    """
    if [ ! -d "${q2cacheDir}" ]; then
      echo "Creating cache for ${_id}..."
      qiime tools cache-create --cache ${q2cacheDir}
    else
      echo "Cache already exists for ${_id}"
    fi

    if [ -f ${q2cacheDir}/keys/${reads_paired} ] && [ -f ${q2cacheDir}/keys/${reads_single} ] && [ -f ${q2cacheDir}/keys/${failed_runs} ]; then
      echo "All cache keys exist for sample ${_id}"
      touch ${reads_paired} && touch ${reads_single} && touch ${failed_runs}
      exit 0
    fi

    if [ ! -d "$HOME/.ncbi" ]; then
        echo 'Directory $HOME/.ncbi does not exist and will be created'
        mkdir $HOME/.ncbi
        echo 'Creating SRA Toolkit config file in $HOME/.ncbi/user-settings.mkfg'
        printf '/LIBS/GUID = "%s"\n' `uuidgen` > $HOME/.ncbi/user-settings.mkfg
    elif [ ! -f "$HOME/.ncbi/user-settings.mkfg" ]; then
        echo 'Creating SRA Toolkit config file in $HOME/.ncbi/user-settings.mkfg'
        printf '/LIBS/GUID = "%s"\n' `uuidgen` > $HOME/.ncbi/user-settings.mkfg
    else
        echo 'NCBI config files exist - we will attempt to copy the config into the QIIME 2 home directory.'
        if [ -d "/home/qiime2" ]; then
          mkdir -p /home/qiime2/.ncbi
          cp $HOME/.ncbi/user-settings.mkfg /home/qiime2/.ncbi/user-settings.mkfg
          ls /home/qiime2/.ncbi
          echo "Success - required config files were created."
#        else
#          echo "The directory /home/qiime2 does not exist - are you running the pipeline using a Singularity container?"
#          exit 1
#        fi
    fi

    echo -e "id\n${_id}" > ids.tsv

    echo "Importing IDs into an artifact..."

    qiime tools import \
      --type NCBIAccessionIDs \
      --input-path ids.tsv \
      --output-path ids.qza

    echo "Starting data fetch..."

    set +e
    qiime fondue get-sequences \
      --verbose \
      --i-accession-ids ids.qza \
      --p-email ${params.email} \
      --p-threads ${task.cpus} \
      --o-single-reads ${q2cacheDir}:${reads_single} \
      --o-paired-reads ${q2cacheDir}:${reads_paired} \
      --o-failed-runs ${q2cacheDir}:${failed_runs} > output.txt 2> error.txt

    qiime_exit_code=\$?
    echo "QIIME exit code: \$qiime_exit_code"
    set -e
    
    cat output.txt >> .command.out
    cat error.txt >> .command.err

    if grep -q "Neither single- nor paired-end sequences could be downloaded" output.txt || grep -q "Neither single- nor paired-end sequences could be downloaded" error.txt; then
      echo "Neither single- nor paired-end sequences could be downloaded."
      touch ${reads_paired} && touch ${reads_single} && touch ${failed_runs}
      exit 125
    fi

    if [[ ${params.fondue.paired} == 'true' ]]; then
      key=${reads_paired}
    else
      key=${reads_single}
    fi

    uuid=\$(awk -F':' '/^[[:space:]]*data[[:space:]]*:/ {val=\$2; gsub(/^[[:space:]]+|[[:space:]]+\$/, "", val); gsub(/^["'"'"']|["'"'"']\$/, "", val); print val; exit}' "${q2cacheDir}/keys/\$key")
    if [[ -z "\$uuid" ]]; then
      echo "Failed to parse cache key metadata from ${q2cacheDir}/keys/\$key"
      exit 1
    fi
    paths=\$(ls ${q2cacheDir}/data/\$uuid/data | grep 'fastq')
    echo "Samples found: \$paths"
    
    if [[ \$paths == *"xxx_"* ]]; then
      echo "Empty sample found for key \$key"
      touch ${reads_paired} && touch ${reads_single} && touch ${failed_runs}
      exit 125
    else
      echo "No empty samples found for key \$key"
    fi

    touch ${reads_paired} && touch ${reads_single} && touch ${failed_runs}

    exit \$qiime_exit_code
    """
}

process SUBSAMPLE_READS {
    label "readSubsampling"
    storeDir params.storeDir
    cpus 1
    scratch true
    tag "${sample_id}"

    input:
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path(reads_subsampled)

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    reads_subsampled = "${params.runId}_reads_subsampled_${params.read_subsampling.fraction.toString().replace(".", "_")}_${sample_id}"
    if (params.read_subsampling.paired) {
      """
      echo Processing sample ${sample_id}
      qiime demux subsample-paired \
        --verbose \
        --i-sequences ${q2cacheDir}:${reads} \
        --p-fraction ${params.read_subsampling.fraction} \
        --o-subsampled-sequences ${q2cacheDir}:${reads_subsampled} \
      && touch ${reads_subsampled}
      """
    } else {
      """
      echo Processing sample ${sample_id}
      qiime demux subsample-single \
        --verbose \
        --i-sequences ${q2cacheDir}:${reads} \
        --p-fraction ${params.read_subsampling.fraction} \
        --o-subsampled-sequences ${q2cacheDir}:${reads_subsampled} \
      && touch ${reads_subsampled}
      """
  }
}

process PROCESS_READS_FASTP {
    label "fastp"
    storeDir params.storeDir
    scratch true
    tag "${sample_id}"
    errorStrategy { task.exitStatus in [125, 126] ? 'ignore' : 'retry' }
    maxRetries 3

    input:
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path(key_reads), path(key_reports)

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    key_reads = "${params.runId}_reads_fastp_${sample_id}"
    key_reports = "${params.runId}_fastp_report_${sample_id}"
    qc_filtering_flag = params.read_qc.fastp.disableQualityFiltering ? "--p-disable-quality-filtering" : "--p-no-disable-quality-filtering"
    dedup_flag = params.read_qc.fastp.deduplicate ? "--p-dedup" : "--p-no-dedup"
    adapter_trimming_flag = params.read_qc.fastp.disableAdapterTrimming ? "--p-disable-adapter-trimming" : "--p-no-disable-adapter-trimming"
    correction_flag = params.read_qc.fastp.enableBaseCorrection ? "--p-correction" : "--p-no-correction"
    """
    echo Processing sample ${sample_id}

    set +e
    qiime fastp process-seqs \
      --verbose \
      --i-sequences ${q2cacheDir}:${reads} \
      ${qc_filtering_flag} \
      ${dedup_flag} \
      ${adapter_trimming_flag} \
      ${correction_flag} \
      ${params.read_qc.fastp.additionalFlags} \
      --p-thread ${task.cpus} \
      --o-processed-sequences ${q2cacheDir}:${key_reads} \
      --o-reports ${q2cacheDir}:${key_reports} > output.txt 2> error.txt

    qiime_exit_code=\$?
    echo "QIIME exit code: \$qiime_exit_code"
    set -e

    cat output.txt >> .command.out
    cat error.txt >> .command.err

    if grep -q "All samples are empty after processing with fastp" output.txt || grep -q "All samples are empty after processing with fastp" error.txt; then
      echo "All reads were removed from this sample - the output was empty."
      touch ${key_reads} && touch ${key_reports}
      exit 125
    elif grep -q "xxx_00" output.txt || grep -q "xxx_00" error.txt; then
      echo "Empty XXX samples found in the data."
      touch ${key_reads} && touch ${key_reports}
      exit 126
    fi

    touch ${key_reads} && touch ${key_reports}

    exit \$qiime_exit_code
    """
}

process VISUALIZE_FASTP {
    cpus 1
    memory { 4.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    time { 2.h * task.attempt }
    publishDir params.publishDir, mode: 'copy'
    scratch true

    input:
    path fastp_reports
    path q2_cache

    output:
    tuple val(viz_label), path("${params.runId}-reads-qc-fastp.qzv"), emit: qzv

    script:
    viz_label = "Reads QC (FastP)"

    """
    qiime fastp visualize \
      --verbose \
      --i-reports ${params.q2cacheDir}:${fastp_reports} \
      --o-visualization ${params.runId}-reads-qc-fastp.qzv
    """
}

process REMOVE_HOST {
    label "hostRemoval"
    label "needsInternet"
    errorStrategy 'retry'
    maxRetries 3
    storeDir params.storeDir
    scratch true
    tag "${sample_id}"
    
    input:
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path(key), emit: reads
    path "human_reference_index", emit: reference, optional: true

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    index_flag = params.databases.hostRemoval.key ? "--i-index ${params.databases.hostRemoval.cache}:${params.databases.hostRemoval.key}" : ""
    key = "${params.runId}_reads_no_host_partitioned_${sample_id}"
    """
    echo Processing sample ${sample_id}

    if [ -f ${params.databases.hostRemoval.cache}/keys/${params.databases.hostRemoval.key} ]; then
      echo Database ${params.databases.hostRemoval.key} already exists in the cache ${params.databases.hostRemoval.cache} and will be used for filtering
      qiime quality-control filter-reads \
        --verbose \
        --i-demultiplexed-sequences ${q2cacheDir}:${reads} \
        --i-database ${params.databases.hostRemoval.cache}:${params.databases.hostRemoval.key} \
        --p-n-threads ${task.cpus} \
        --p-mode ${params.host_removal.mode} \
        --p-sensitivity ${params.host_removal.sensitivity} \
        --p-ref-gap-open-penalty ${params.host_removal.ref_gap_open_penalty} \
        --p-ref-gap-ext-penalty ${params.host_removal.ref_gap_ext_penalty} \
        --o-filtered-sequences ${q2cacheDir}:${key}
    elif [[ "${params.host_removal.human}" == "true" ]]; then
      echo Database ${params.databases.hostRemoval.key} does not exist in the cache ${params.databases.hostRemoval.cache} and will be constructed by the "filter-reads-pangenome" action
      qiime annotate filter-reads-pangenome \
        --verbose \
        --i-reads ${q2cacheDir}:${reads} \
        --p-n-threads ${task.cpus} \
        --p-mode ${params.host_removal.mode} \
        --p-sensitivity ${params.host_removal.sensitivity} \
        --p-ref-gap-open-penalty ${params.host_removal.ref_gap_open_penalty} \
        --p-ref-gap-ext-penalty ${params.host_removal.ref_gap_ext_penalty} \
        --o-filtered-reads ${q2cacheDir}:${key} \
        ${index_flag} \
        --o-reference-index ${params.databases.hostRemoval.cache}:human_reference_index > output.log 2>&1 \
      && touch human_reference_index
    else
      echo Database ${params.databases.hostRemoval.key} does not exist in the cache ${params.databases.hostRemoval.cache} - please provide a key to an exisitng database or toggle the "human" option to "true"
      exit 1
    fi
    
    touch ${key}
    """
}

process INIT_CACHE {

    output:
    path "cache.txt"

    script:
    if (params.q2cacheDirExists == "ok"){
      """
      qiime tools cache-create --cache ${params.q2cacheDir}
      echo ${params.q2cacheDir} > cache.txt
      """
    } else {
      """
      if [ -d "${params.q2cacheDir}" ]; then
        echo "Indicated QIIME 2 cache directory exists. Exiting."
        exit 1
      else
        qiime tools cache-create --cache ${params.q2cacheDir}
        echo ${params.q2cacheDir} > cache.txt
      fi
      """
    }
    
}

process FETCH_ARTIFACT {
    publishDir params.publishDir, mode: 'copy'
    memory { 4.GB * task.attempt }
    time { 2.h * task.attempt }
    maxRetries 3
    errorStrategy 'retry'

    input:
    val cache_key
    
    output:
    path artifact_name

    script:
    cache_key = new File(cache_key.toString()).getName();
    artifact_name = cache_key.replace("_", "-") + ".qza"
    """
    qiime tools cache-fetch \
      --cache ${params.q2cacheDir} \
      --key ${cache_key} \
      --output-path ${artifact_name}
    """
}

process IMPORT_READS {
    tag "${_id}"
    scratch true
    errorStrategy 'retry'
    maxRetries 3
    memory { 2.GB * task.attempt }
    time { 2.h * task.attempt }

    input:
    tuple val(_id), path(reads_fwd), path(reads_rev)

    output:
    tuple val(_id), path(key)

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${_id}"
    key = "${params.runId}_reads_${_id}"
    """
    echo "Creating cache for ${_id}..."
    if [ ! -d "${q2cacheDir}" ]; then
      qiime tools cache-create --cache ${q2cacheDir}
    fi

    echo "Creating manifest file..."
    if [ -n "${reads_rev}" ]; then
        echo -e "sample-id\tforward-absolute-filepath\treverse-absolute-filepath" > manifest.tsv
        echo -e "${_id}\t\$(readlink -f ${reads_fwd})\t\$(readlink -f ${reads_rev})" >> manifest.tsv
        semanticType="PairedEndSequencesWithQuality"
        dataFormat="PairedEndFastqManifestPhred33V2"
    else
        echo -e "sample-id\tforward-absolute-filepath" > manifest.tsv
        echo -e "${_id}\t\$(readlink -f ${reads_fwd})" >> manifest.tsv
        semanticType="SequencesWithQuality"
        dataFormat="SingleEndFastqManifestPhred33V2"
    fi
    
    echo "Importing reads..."
    qiime tools cache-import \
      --cache ${q2cacheDir} \
      --key ${key} \
      --type "SampleData[\$semanticType]" \
      --input-path manifest.tsv \
      --input-format \$dataFormat
    
    touch ${key}
    """
}

process PARTITION_DEREP_MAGS {
    cpus 1
    storeDir params.storeDir
    time { 2.h * task.attempt }
    memory { 4.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    scratch true

    input:
    path mags_derep
    path q2_cache

    output:
    path "${params.runId}_mags_${params.binning.primary}_derep_partitioned_*"

    script:
    """
    uuid=\$(awk -F':' '/^[[:space:]]*data[[:space:]]*:/ {val=\$2; gsub(/^[[:space:]]+|[[:space:]]+\$/, "", val); gsub(/^["'"'"']|["'"'"']\$/, "", val); print val; exit}' "${params.q2cacheDir}/keys/${mags_derep}")
    if [[ -z "\$uuid" ]]; then
      echo "Failed to parse cache key metadata from ${params.q2cacheDir}/keys/${mags_derep}"
      exit 1
    fi
    mags=\$(ls ${params.q2cacheDir}/data/\$uuid/data/*.{fa,fasta} 2>/dev/null | xargs -n 1 basename | sed -E 's/\\.(fa|fasta)\$//')
    mkdir -p "${params.q2TemporaryCachesDir}/mags"

    # Convert MAGs to array and process in specified number of batches
    readarray -t mag_array <<< "\${mags}"
    total_mags=\${#mag_array[@]}
    batch_count=${params.functional_annotation.partitionBatchCount}
    
    # Calculate batch size (ceiling division)
    batch_size=\$(( (total_mags + batch_count - 1) / batch_count ))

    for ((batch_id=0; batch_id<\${batch_count}; batch_id++)); do
      start_idx=\$((batch_id * batch_size))
      
      # Skip if we've processed all MAGs
      if [ \$start_idx -ge \$total_mags ]; then
        break
      fi
      
      end_idx=\$((start_idx + batch_size))
      if [ \$end_idx -gt \$total_mags ]; then
        end_idx=\$total_mags
      fi
      
      q2cacheDir="${params.q2TemporaryCachesDir}/mags/batch_\${batch_id}"
      key="${params.runId}_mags_${params.binning.primary}_derep_partitioned_\${batch_id}"
      
      if [ ! -d \$q2cacheDir ]; then
        echo "Creating cache \$q2cacheDir..."
        qiime tools cache-create --cache \$q2cacheDir
      fi

      # Create metadata.tsv with all MAGs in this batch
      echo "id" > metadata.tsv
      for ((j=start_idx; j<\${end_idx}; j++)); do
        echo "\${mag_array[j]}" >> metadata.tsv
      done

      echo "Filtering batch \${batch_id} (\$((end_idx - start_idx)) MAGs)..."
      cat metadata.tsv

      qiime mag filter-derep-mags \
        --verbose \
        --i-mags ${params.q2cacheDir}:${mags_derep} \
        --m-metadata-file metadata.tsv \
        --o-filtered-mags \$q2cacheDir:\$key
      
      touch \$key
    done
    """  
}

process COLLATE_PARTITIONS {
    label "collation"
    cpus 1
    time { 2.h * task.attempt }
    memory { 2.GB * task.attempt }
    errorStrategy 'retry'
    storeDir params.storeDir
    maxRetries 3
 
    input:
    val id_and_paths
    val cache_key_out 
    val qiime_action
    val qiime_input_flag
    val qiime_output_flag
    val clean_up

    output:
    path "${cache_key_out}"

    script:
    def inputString = id_and_paths.collect { item ->
        def sample_id = item[0]
        def path = item[1]
        def key = new File(path.toString()).getName()
        "${params.q2TemporaryCachesDir}/${sample_id}:${key}"
    }.join(' ')
  
    """
    echo "Combined input: ${inputString}"
    
    qiime ${qiime_action} \
      ${qiime_input_flag} ${inputString} \
      ${qiime_output_flag} ${params.q2cacheDir}:${cache_key_out} \
    && touch ${cache_key_out}
    """

    // if (clean_up === true) {
    //   """
    //   qiime tools cache-remove --cache ${params.q2cacheDir} --key ${prefix}_collection
    //   """
    // }
}

process COLLATE_PARTITIONS_DEREP {
    label "collation"
    cpus 1
    time { 2.h * task.attempt }
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'

    input:
    val id_and_paths
    val cache_key_out 
    val qiime_action
    val qiime_input_flag
    val qiime_output_flag
    val clean_up

    output:
    path "${cache_key_out}"

    script:
    def inputString = id_and_paths.collect { item ->
        def sample_id = item[0]
        def path = item[1]
        def key = new File(path.toString()).getName()
        "${params.q2TemporaryCachesDir}/mags/${sample_id}:${key}"
    }.join(' ')
  
    """
    echo "Combined input: ${inputString}"
    
    qiime ${qiime_action} \
      ${qiime_input_flag} ${inputString} \
      ${qiime_output_flag} ${params.q2cacheDir}:${cache_key_out} \
      --use-cache ${params.q2cacheDir} \
    && touch ${cache_key_out}
    """

    // if (clean_up === true) {
    //   """
    //   qiime tools cache-remove --cache ${params.q2cacheDir} --key ${prefix}_collection
    //   """
    // }
}

process TABULATE_READ_COUNTS {
    storeDir params.storeDir
    scratch true
    tag "${sample_id}"
    time { 2.h * task.attempt }
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    
    input:
    tuple val(sample_id), path(reads)

    output:
    tuple val(sample_id), path(key)

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    key = "${params.runId}_reads_counts_${sample_id}"
    """
    echo Processing sample ${sample_id}
    qiime demux tabulate-read-counts \
      --verbose \
      --i-sequences ${q2cacheDir}:${reads} \
      --o-counts ${q2cacheDir}:${key} \
      && touch ${key}
    """
}

process TABULATE_READ_COUNTS_BATCH {
    cpus 1
    time { 4.h * task.attempt }
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    tag "${step}"
    publishDir params.traceDir, mode: 'copy'

    input:
    val samples
    val step

    output:
    tuple val(step), path("${step}_read_counts.tsv")

    script:
    def samplesData = samples.collect { item ->
        [sample_id: item[0].toString(), reads_key: new File(item[1].toString()).getName()]
    }
    def samplesJson = groovy.json.JsonOutput.toJson(samplesData)
    """
python3 - <<'PY'
import os, json, gzip

samples = ${samplesJson}
q2_tmp_dir = "${params.q2TemporaryCachesDir}"

def uuid_from_key(cache_dir, key):
    key_file = os.path.join(cache_dir, "keys", key)
    with open(key_file) as fh:
        for line in fh:
            if line.strip().startswith("data:"):
                return line.split(":", 1)[1].strip().strip("'\\\"")
    raise RuntimeError(f"No 'data:' field found in {key_file}")

def count_reads_fastq(fastq_files):
    total = 0
    for fq in fastq_files:
        opener = gzip.open if fq.endswith(".gz") else open
        with opener(fq, "rt") as fh:
            total += sum(1 for _ in fh) // 4
    return total

print("sample_id\\tcount")
with open("${step}_read_counts.tsv", "w") as out:
    out.write("sample_id\\tcount\\n")
    for s in samples:
        sample_id = s["sample_id"]
        reads_key = s["reads_key"]
        cache_dir = os.path.join(q2_tmp_dir, sample_id)
        uuid = uuid_from_key(cache_dir, reads_key)
        data_dir = os.path.join(cache_dir, "data", uuid, "data")
        fastq_files = sorted(
            os.path.join(data_dir, f)
            for f in os.listdir(data_dir)
            if f.endswith(".fastq.gz") or f.endswith(".fastq")
        )
        n = count_reads_fastq(fastq_files)
        print(f"  {sample_id}: {n} reads", flush=True)
        out.write(f"{sample_id}\\t{n}\\n")

print("Done")
PY
    """
}

process MAKE_SAMPLE_REPORT {
    cpus 1
    memory { 1.GB * task.attempt }
    time { 30.min * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    publishDir params.traceDir, mode: 'copy'

    input:
    val metrics

    output:
    path "${params.runId}_sample_report_mqc.json"

    script:
    def metricsJson = groovy.json.JsonOutput.toJson(
        metrics.collect { item -> [label: item[0].toString(), count: item[1] as int] }
    )
    """
python3 - <<'PY'
import json

metrics = ${metricsJson}
data = {m["label"]: {"count": m["count"]} for m in metrics}
report = {
    "id": "moshpit_sample_counts",
    "plot_type": "barplot",
    "pconfig": {
        "id": "sample_counts_plot",
        "title": "Samples Retained",
        "ylab": "# Samples",
    },
    "data": data,
}

with open("${params.runId}_sample_report_mqc.json", "w") as out:
    json.dump(report, out, indent=2)

print(f"Written sample report with {len(data)} metrics")
PY
    """
}

process MULTIQC {
    cpus 1
    memory { 2.GB * task.attempt }
    time { 30.min * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    publishDir "${params.publishDir}/multiqc", mode: 'copy'

    input:
    path sample_report
    path read_counts_report

    output:
    path "multiqc_report.html"
    path "multiqc_report_data"

    script:
    """
    multiqc . --filename multiqc_report --force
    """
}

process REPORT_READ_COUNTS {
    cpus 1
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    time { 1.h * task.attempt }
    publishDir params.traceDir, mode: 'copy'

    input:
    val step_tsvs

    output:
    path "${params.runId}_read_counts_mqc.json"

    script:
    def tsvListJson = groovy.json.JsonOutput.toJson(
        step_tsvs.collect { item -> [step: item[0].toString(), tsv: item[1].toString()] }
    )
    """
python3 - <<'PY'
import json

step_order = ["input", "subsampled", "fastp", "host_removed", "filtered"]

entries = ${tsvListJson}

counts = {}
observed_steps = set()

for entry in entries:
    step = entry['step']
    tsv_path = entry['tsv']
    observed_steps.add(step)

    with open(tsv_path) as tf:
        lines = [l.rstrip('\\n') for l in tf if l.strip()]

    if not lines:
        continue
    header = lines[0].split('\\t')
    for line in lines[1:]:
        row = dict(zip(header, line.split('\\t')))
        sid = row.get('sample_id')
        cnt = row.get('count')
        if sid and cnt:
            counts.setdefault(sid, {})[step] = int(cnt)

active_steps = [s for s in step_order if s in observed_steps]

headers = {}
for step in active_steps:
    headers[step] = {
        "title": step.replace("_", " ").capitalize(),
        "description": f"Read count after {step} step",
        "format": "{:,.0f}",
        "min": 0,
        "scale": "Blues",
    }
    pct_key = f"{step}_pct"
    headers[pct_key] = {
        "title": f"{step.replace('_', ' ').capitalize()} %",
        "description": f"Reads retained after {step} step relative to input",
        "format": "{:.1f}",
        "suffix": "%",
        "min": 0,
        "max": 100,
        "scale": "RdYlGn",
    }

data = {}
for sample_id, step_counts in sorted(counts.items()):
    row = {}
    input_count = step_counts.get("input")
    for step in active_steps:
        n = step_counts.get(step)
        if n is not None:
            row[step] = n
            row[f"{step}_pct"] = round(n / input_count * 100, 2) if input_count else None
    data[sample_id] = row

report = {
    "id": "moshpit_read_counts",
    "section_name": "Read Counts per Processing Step",
    "description": "Number of reads per sample at each read processing stage. Percentages are relative to the initial input.",
    "plot_type": "table",
    "pconfig": {
        "id": "read_counts_table",
        "title": "Read Counts per Processing Step",
    },
    "headers": headers,
    "data": data,
}

with open("${params.runId}_read_counts_mqc.json", "w") as out:
    json.dump(report, out, indent=2)

print(f"Written with {len(data)} samples and {len(active_steps)} steps")
PY
    """
}

process FILTER_SAMPLES {
    errorStrategy { task.exitStatus == 125 ? 'ignore' : 'retry' }
    storeDir params.storeDir
    scratch true
    tag "${sample_id}"
    time { 2.h * task.attempt }
    memory { 2.GB * task.attempt }
    maxRetries 3
    
    input:
    tuple val(sample_id), path(reads), path(metadata)
    val query
    val should_partition

    output:
    tuple val(sample_id), path(key)

    script:
    if (should_partition) {
      q2cacheDirIn = params.inputReadsCache
      q2cacheDirOut = "${params.q2TemporaryCachesDir}/${sample_id}"
      key = "${params.runId}_reads_partitioned_${sample_id}"
    } else {
      q2cacheDirIn = "${params.q2TemporaryCachesDir}/${sample_id}"
      q2cacheDirOut = "${params.q2TemporaryCachesDir}/${sample_id}"
      key = "${params.runId}_reads_filtered_${sample_id}"
    }
    if (should_partition) {
      """
      echo Creating partition for sample ${sample_id}

      echo "id" > _metadata.tsv
      echo "${sample_id}" >> _metadata.tsv

      if [ ! -d "${q2cacheDirOut}" ]; then
        qiime tools cache-create --cache ${q2cacheDirOut}
      fi

      qiime demux filter-samples \
        --verbose \
        --i-demux ${q2cacheDirIn}:${reads} \
        --m-metadata-file _metadata.tsv \
        --o-filtered-demux ${q2cacheDirOut}:${key}

      touch ${key}
      """
    } else {
      """
      echo Processing sample ${sample_id}

      set +e
      qiime demux filter-samples \
        --verbose \
        --i-demux ${q2cacheDirIn}:${reads} \
        --m-metadata-file ${q2cacheDirIn}:${metadata} \
        --p-where ${query} \
        --o-filtered-demux ${q2cacheDirOut}:${key} > output.txt 2> error.txt
      
      qiime_exit_code=\$?
      echo "QIIME exit code: \$qiime_exit_code"
      set -e

      cat output.txt >> .command.out
      cat error.txt >> .command.err

      if grep -q "No filtering requested" output.txt || grep -q "No filtering requested" error.txt; then
        echo "The generated artifact did not contain any samples."
        exit 125
      fi

      touch ${key}

      exit \$qiime_exit_code
      """
    }
}

process CLEAN_UP_CACHES {
    time { 2.h * task.attempt }
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    
    input:
    val dependency
    val path_to_remove

    script:
    """
    echo "Removing ${path_to_remove}"
    rm -rf ${path_to_remove}
    """
}

process MAKE_REPORT {
    cpus 1
    memory { 2.GB * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    time { 1.h * task.attempt }
    publishDir params.publishDir, mode: 'copy'
    scratch true

    input:
    val visualizations

    output:
    path "${params.runId}-report.qzv"

    script:
    def visualization_json = groovy.json.JsonOutput.toJson(
        visualizations.collect { [label: it[0].toString(), path: it[1].toString()] }
    )
    """
    cat <<'EOF' > visualizations.json
    ${visualization_json}
    EOF

    python - <<'PY'
    import json
    from qiime2 import Visualization
    from q2templates.reports import matryoshka_template

    with open('visualizations.json') as handle:
        entries = json.load(handle)

    visualizations = {
        entry["label"]: Visualization.load(entry["path"])
        for entry in entries
    }

    viz = Visualization.make_report(matryoshka_template, visualizations)
    viz.save('${params.runId}-report.qzv')
    print('Visualization saved to "${params.runId}-report.qzv"')
    PY
    """
}

process ARCHIVE_SAMPLE_CACHE {
    tag "${sample_id}"
    cpus 1
    memory { 2.GB * task.attempt }
    time { 4.h * task.attempt }
    maxRetries 2
    errorStrategy 'retry'

    input:
    val sample_id
    val ready

    output:
    val sample_id, emit: archived

    script:
    q2cacheDir = "${params.q2TemporaryCachesDir}/${sample_id}"
    archivePath = "${params.archiveDir}/${sample_id}.zip"
    """
    set -euo pipefail
    echo "=== Archiving cache for sample ${sample_id} ==="

    if [ ! -d "${q2cacheDir}" ]; then
        echo "Cache directory not found: ${q2cacheDir} - skipping."
        exit 0
    fi

    mkdir -p ${params.archiveDir}

    cd ${params.q2TemporaryCachesDir}
    zip -rq ${archivePath} ${sample_id}/

    echo "Verifying archive integrity..."
    if ! unzip -tq ${archivePath}; then
        echo "ERROR: Archive CRC check failed!"
        rm -f ${archivePath}
        exit 1
    fi

    original_count=\$(find ${q2cacheDir} -type f | wc -l)
    archive_count=\$(zipinfo -1 ${archivePath} | grep -cv '/\$' || true)
    echo "Files: original=\${original_count}, archive=\${archive_count}"

    if [ "\${archive_count}" -lt "\${original_count}" ]; then
        echo "ERROR: Archive has fewer files than original!"
        rm -f ${archivePath}
        exit 1
    fi

    echo "Verification passed - removing original cache."
    rm -rf ${q2cacheDir}
    echo "=== Done: ${archivePath} ==="
    """
}

process REMOVE_FROM_CACHE {
    tag "${sample_id}"
    cpus 1
    memory { 500.MB * task.attempt }
    time { 30.min * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    
    input:
    tuple val(sample_id), val(artifact_path)

    script:
    key = new File(artifact_path.toString()).getName()
    """
    echo "Removing ${key} from ${sample_id} cache..."
    qiime tools cache-remove \
      --cache ${params.q2TemporaryCachesDir}/${sample_id} \
      --key ${key}
    """
}

process FIX_CACHE_PERMISSIONS {
    tag "${id}"
    cpus 1
    memory { 500.MB * task.attempt }
    time { 1.h * task.attempt }
    maxRetries 3
    errorStrategy 'retry'
    
    input:
    tuple val(id), val(path), val(ready_signal)

    output:
    val id

    script:
    """
    CACHE="${path}"
    # Normalize to avoid surprises with trailing slashes
    CACHE="\${CACHE%/}"
    
    if [ ! -d "\${CACHE}" ]; then
        echo "Error: cache directory does not exist: \${CACHE}" >&2
        # We don't exit with error here to avoid failing the pipeline if the cache was already cleaned up or doesn't exist for some reason
        exit 0
    fi

    # Top level cache directory needs to be writable by the group
    echo "Updating top-level permissions on '\${CACHE}' (g+rw)"
    chmod g+rw "\${CACHE}"

    # Pools and processes need to be writable by the group (if present)
    echo "Updating permissions on 'pools' and 'processes' within '\${CACHE}' (g+rw)"
    [[ -d "\${CACHE}/pools" ]]     && chmod -R g+rw "\${CACHE}/pools"
    [[ -d "\${CACHE}/processes" ]] && chmod -R g+rw "\${CACHE}/processes"

    # Data and keys need to be readable by the group (if present)
    echo "Updating permissions on the 'keys' within '\${CACHE}' (g+r)"
    [[ -d "\${CACHE}/keys" ]] && chmod -R g+r "\${CACHE}/keys"
    echo "Updating permissions on the 'data' within '\${CACHE}' (g+rx)"
    [[ -d "\${CACHE}/data" ]] && chmod -R g+rx "\${CACHE}/data"

    echo "All done!"
    """
}
